plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace rootProject.libIdCompose
    compileSdk rootProject.targetSdk

    defaultConfig {
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.java
        targetCompatibility rootProject.java
    }
    kotlinOptions {
        jvmTarget = rootProject.jvmTarget
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion rootProject.composeCompiler
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

java {
    sourceCompatibility rootProject.java
    targetCompatibility rootProject.java
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

dependencies {

    implementation "androidx.core:core-ktx:$rootProject.core"
    implementation "androidx.compose.ui:ui:$rootProject.compose"
    implementation "androidx.compose.material:material:$rootProject.compose"
    implementation "androidx.compose.foundation:foundation:$rootProject.compose"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompat"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.groupId
            artifactId = rootProject.libArtifactCompose
            version = rootProject.libVersion

            afterEvaluate {
                from components.release
            }
        }
    }
}